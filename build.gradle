buildscript {

    apply from: "dependencies.gradle"

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath classpaths.gradlePlugin
        classpath classpaths.kotlin
        classpath classpaths.kotlinAndroidExt
    }
}

plugins {
    id "scabbard.gradle" version "0.5.0"
}

scabbard {
    enabled true
    outputFormat "png"
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

subprojects {

    if (project.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {

        compileSdkVersion configs.compileSdk

        defaultConfig {

            minSdkVersion configs.minSdk
            targetSdkVersion configs.targetSdk

            if (project.name == 'app') {
                applicationVariants.all { variant ->
                    variant.outputs.all {
                        outputFileName = "app-${variant.name}-${variant.versionName}.apk"
                    }
                }
            }
        }

    }

    dependencies {

        api libs.dagger
        kapt libs.daggerCompiler

        // Kotlin
        implementation libs.kotlin

        // Logging
        implementation libs.timber

        // Unit tests
        testImplementation testLibs.junit
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
